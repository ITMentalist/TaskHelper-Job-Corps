''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Procedures_General (Module)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Option Explicit

'Tag for the user's home page
    Const tagHome As String = " BNIMAIN"
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RestrictToPositiveWholeNumbers
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub RestrictToPostiveWholeNumbers(KeyAscii As MSForms.ReturnInteger)

Select Case KeyAscii
     Case Asc("0") To Asc("9")
     Case Else
         KeyAscii = 0
 End Select

End Sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RestrictToPostiveFloatNumbers
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub RestrictToPostiveFloatNumbers(KeyAscii As MSForms.ReturnInteger, str_textbox As String)

Select Case KeyAscii
     Case Asc("0") To Asc("9")
     Case Asc(".")
         If InStr(1, str_textbox, ".") > 0 Then
             KeyAscii = 0
         End If
     Case Else
         KeyAscii = 0
 End Select

End Sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' TurnOffExcelDefaults
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub TurnOffExcelDefaults(Optional ByRef Exception As cException)

    'Pass error handling up the call stack
    On Error GoTo 0
    If Not Exception Is Nothing Then Exception.Push "Procedures_General.TurnOffExcelDefaults"

    Application.ScreenUpdating = False
    Application.DisplayAlerts = False
    Application.Calculation = xlCalculationManual
    
Cleanup:
    
    If Not Exception Is Nothing Then Exception.Pop
    
End Sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' TurnOnExcelDefaults
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub TurnOnExcelDefaults(Optional ByRef Exception As cException)

    'Pass error handling up the call stack
    On Error GoTo 0
    If Not Exception Is Nothing Then Exception.Push "Procedures_General.TurnOnExcelDefaults"
    
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    Application.ScreenUpdating = True
    
Cleanup:
    
    If Not Exception Is Nothing Then Exception.Pop
    
End Sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' NavigateHome
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub NavigateHome(ByVal AS400 As cAS400, ByRef Exception As cException)

    'Pass error handling up the call stack
    On Error GoTo 0
    Exception.Push "Procedures_General.NavigateHome"
    
    Dim i As Integer                        'Loop counter
    Const maxAttempts As Integer = 5        'Max loop count
    Const rowHome As Long = 19              'Navigation entry
    Const colHome As Long = 7               'Navigation entry
    Const tagLength As Long = 8             'Location of page tag
    Const tagRow As Long = 1                'Location of page tag
    Const tagCol As Long = 2                'Location of page tag
    
    Exception.ErrMessage = "Starting the navigation loop"
    Do
    
        AS400.Pause
        
        Select Case AS400.GetText(tagRow, tagCol, tagLength)
        
            'PS: BNIMAIN (Destination)
            Case Is = tagHome
                'Note: Certain screens have a "pop-up" screen _
                    that must be F3'd out of as well
                AS400.SendKeys F3Key
                Exit Do
                
            'PS: BNICS
            Case " BNICS  "
                AS400.SendKeys F12Key
                Exit Do
                
            'PS: 66300
            Case " 66300  "
                AS400.SendKeys F3Key
                
            'PS: 66340
            Case " 66340  "
                AS400.SendKeys F3Key
                
            'PS: Student Service Inquiry (1.14.)
            Case "603131_B"
                AS400.SendKeys F3Key
            
            'Address Book Information
            Case "01051   "
                AS400.SendKeys F3Key
                
            'PS: (1.14.), Customer Ledger Inquiry
            Case " 032002 "
                AS400.SendKeys F3Key

            'PS: (1.14.), ICS Cancellation Maintenance
            Case "631001  "
                AS400.SendKeys F3Key
                
            'PS: (1.14.), Student Supplemental
            Case "60110   "
                AS400.SendKeys F3Key

            'PS: (1.14.), Student Course Master
            Case "60210   "
                AS400.SendKeys F3Key
                
            Case "60211   "
                AS400.SendKeys F3Key
                AS400.SendKeys F3Key

            'PS: (1.14.), Shipping Screen
            Case "42045   "
                AS400.SendKeys F3Key

            'PS: ICS Accounting Main Menu
            Case " ICSACCT"
                AS400.SendKeys F12Key

            'PS: Main Menu
            Case "ICSMAIN "
                AS400.Pause
                AS400.SetText "6", 20, 7
                AS400.SendKeys EnterKey

            'Library List Selection
            Case " 98VL   "
                AS400.Pause
                AS400.SetText "1", 6, 10
                AS400.SendKeys EnterKey

            'Product File Maintenance
            Case " BNIPROD"
                AS400.SendKeys F12Key
                
            'Note Maintenance
            Case " 55192  "
                AS400.SendKeys F3Key
                
            'Education Screen
            Case " 55192  "
                AS400.SendKeys F3Key
                
            'Name Search
            Case "603132_B"
                AS400.SendKeys F3Key
                
            Case "631001  "
                AS400.SendKeys F3Key
                AS400.SendKeys F3Key
                
            Case " BNISOP "
                AS400.SendKeys F12Key
                AS400.SendKeys F12Key
                
            Case " 42040  "
                AS400.SendKeys F3Key
                AS400.SendKeys F12Key
                AS400.SendKeys F12Key
                
            'Empty
            Case "          "

                Select Case AS400.GetText(1, 32, 10)
                    
                    'Exit
                    Case "       Exi"
                        AS400.SendKeys EnterKey
                    
                    'Sign-on Information
                    Case "ign-on Inf"
                        AS400.SendKeys EnterKey
                        AS400.SendKeys EnterKey
                        
                    'Display Program Messages
                    Case "lay Progra"
                        AS400.SendKeys EnterKey
                        AS400.SendKeys EnterKey
                    
                    'Display Messages (Standard screen)
                    Case " Display M"
                        AS400.SendKeys EnterKey
                        
                    'Sign On"
                    Case "    Sign O"
                        Exception.Flag = True
                        MsgBox "Please sign into the AS/400 before proceeding", _
                            vbInformation, "Sign-In required"
                        Exit Do
                    
                    'MSM screens
                    Case "          "
                        Exception.Flag = True
                        MsgBox "Please ensure the AS/400 is signed into the current environment " & _
                            "before proceeding", vbInformation, "Incorrect AS/400 location"
                        Exit Do
                        
                End Select
                
            Case Else
            
                AS400.SendKeys F3Key
                AS400.SendKeys F12Key

        End Select
        
        i = i + 1
        
        Exception.ErrMessage = " ... ... Failed attempts: " & i
        
        Loop While i <= maxAttempts
        
Cleanup:

    AS400.Pause
    
    Exception.Pop
    
End Sub

Public Function HomeScreenConfirmed(ByVal AS400 As cAS400, ByRef Exception As cException) As Boolean

    'Passes error handling up the call stack
    On Error GoTo 0
    If Not Exception Is Nothing Then Exception.Push "Procedures_General.ConfirmHomeScreen"
    
    HomeScreenConfirmed = AS400.SearchText(Trim(tagHome), 1, 1, forward)
    
    Exception.Pop
    
End Function

Public Function CheckString(ByVal result_to_save As String, Optional ByVal extra_spaces As Integer = 5) As String

    Dim lenCount As Integer
    
    lenCount = Len(Trim(result_to_save))
    If lenCount >= 1 Then
        CheckString = Trim(result_to_save)
    Else
        CheckString = Space(extra_spaces) & "(Empty)"
    End If
    
End Function
