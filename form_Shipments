''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' form_Shipments (userform)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Option Explicit

'RGB for an unselected textbox
    Const lockedRed As Integer = 211
    Const lockedGreen As Integer = 211
    Const lockedBlue As Integer = 211

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Initialization event
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Private Sub UserForm_Initialize()

    'To generate a list of active sessions
    Dim objAS400ConnList As Object
    Dim i As Integer
    Dim container As String
    
    SID_Textbox = vbNullString
    Status_Textbox = vbNullString
    
    Status_Textbox.Locked = True
    Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
    Status_Textbox = "Select a shipment option." & Space(5) & _
        "(Please ensure the session and Student ID values are correct before continuing)"
    
    'For defaulting the session and Student ID values
    If DebugFlag Then On Error GoTo 0 Else On Error Resume Next
    Set rnglastSession = Range("lastSession")
    Set rngStudentID = Range("lastStudentID")
    SID_Textbox = Trim(rngStudentID.Value)
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo 0
    
    'Connects to the AS/400 to generate a list of active session names
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo AS400NotActive
    Set objAS400ConnList = CreateObject("PCOMM.autECLConnList")
    
    objAS400ConnList.Refresh
    
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo 0
    container = Trim(Range("DefaultSession"))
    'Populates the selection boxes
    With Session_ComboBox
    
        .Clear
        
        If Len(Trim(rnglastSession)) = 1 Then
            .AddItem rnglastSession
        End If
        
        If Len(container) = 1 And InStr(1, "ABCDEFGHIJKLMNOPQRSTUVWXYZ", container) >= 1 Then
            .AddItem container
        End If
        
        For i = 1 To objAS400ConnList.Count
            .AddItem objAS400ConnList(i).name
        Next i
        
        .Text = Session_ComboBox.List(0)
        
    End With
    
    Set objAS400ConnList = Nothing
    
    'Sets the initial focus
    SID_Textbox.SetFocus
    
Exit Sub

ObjectNotFound:

    SID_Textbox = vbNullString
    
Resume Next

AS400NotActive:

    MsgBox "Note: The AS/400 has not been detected", vbInformation, "Attempting to connect to the AS/400"
    
Resume Next
    
End Sub

Private Sub button_FullRelease_Click()

    Status_Textbox.BackColor = &HFFFF00
    Status_Textbox = "Releasing all shipments ..."

    If MsgBox("This will RELEASE every shipment on the account." & vbNewLine & vbNewLine & _
        "Do you wish to proceed?", vbYesNo, "Shipment Selection: Release All Shipments") = vbYes Then
        
        Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
        RunShipmentSelection form_Shipments, True
        
    Else
    
        Status_Textbox = "Select a shipment option."
        Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
    
    End If
    
End Sub

Private Sub button_FullStop_Click()

    Status_Textbox.BackColor = &H8080FF
    Status_Textbox = "Stopping all shipments ..."

    If MsgBox("This will STOP every shipment on the account." & vbNewLine & vbNewLine & _
            "Do you wish to proceed?", vbYesNo, "Shipment Selection: Stop All Shipments") = vbYes Then
        
        Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
        RunShipmentSelection form_Shipments, False
    
    Else
    
        Status_Textbox = "Select a shipment option."
        Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
    
    End If
    
End Sub

Private Sub button_NoAction_Click()

    Status_Textbox.BackColor = &HC0FFFF
    Status_Textbox = "Closing the form ..."
    
    If MsgBox("This selection will close the Shipments form." & vbNewLine & vbNewLine & _
            "Do you wish to close the form?", vbYesNo, "Shipment Selection: Perform No Action") = vbYes Then

        Unload Me
        
    Else
    
        Status_Textbox = "Select a shipment option."
        Status_Textbox.BackColor = RGB(lockedRed, lockedGreen, lockedBlue)
    
    End If

End Sub

Private Sub SID_Textbox_KeyPress(ByVal KeyAscii As MSForms.ReturnInteger)
    RestrictToPostiveWholeNumbers KeyAscii
End Sub
