
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' m_Shipments (Module)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Option Explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ManageShipments
'Assumes the passed arguments are valid entries
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub ManageShipments(ByVal AS400 As cAS400, ByVal SID As String, ByVal SendShipments As Boolean, ByRef Exception As cException)

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' General variables
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Exception.Push "Procedure: m_Shipments.ManageShipments"
    
    Const StartSize = 12
    Const MaxSize = 23
    
    Dim GetTextString As String
    GetTextString = vbNullString
    
    Dim CheckForTarget As String
    CheckForTarget = vbNullString
    
    'The AS/400 only accepts values as long
    Dim i As Long
    i = 0
    
    Dim SetCount As Integer
    SetCount = 0
    
    Dim Pages As Integer
    Pages = 0
    
    Dim Target As String
    Target = vbNullString
    
    Dim EndPageLoop As Boolean
    EndPageLoop = False
    
    Dim ShipSet() As Variant
    ShipSet() = Array("540", "560", "620")
    

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Loads the required objects/information
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    TurnOffExcelDefaults Exception

    AS400.Handling = "Fast"
    AS400.Milliseconds = 50
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Navigation
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    On Error GoTo NavigationError
    GetTextString = AS400.Presentation.GetText(1, 3, 5)
    
    If Not GetTextString = "42040" Then

        If NavigateToHome(AS400, Exception) = False Then GoTo NavigationFailure
    
        AS400.Presentation.SetText "1", 19, 7
        AS400.Presentation.SendKeys "[Enter]"
        WaitForReady AS400, Exception
    
        AS400.Presentation.SetText "8", 19, 7
        AS400.Presentation.SendKeys "[Enter]"
        WaitForReady AS400, Exception
        
    Else
        AS400.Presentation.SendKeys "[pf3]"
    End If
    
    If SendShipments = False Then
        AS400.Presentation.SetText "17", 19, 7
    Else
        AS400.Presentation.SetText "18", 19, 7
    End If
    
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Option: "Prevent Shipment Release"
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    On Error Resume Next
    If SendShipments = False Then
           
        '1st level loop: Perform on each shipment status
        For SetCount = LBound(ShipSet) To UBound(ShipSet)
        
            Select Case ShipSet(SetCount)
            
                Case "540"
                    Target = "539"
                Case "560"
                    Target = "540"
                Case "620"
                    Target = "560"
                Case Else
                    GoTo ArrayFailure
                    
            End Select
        
            AS400.Presentation.SetText SID, 4, 22
            AS400.Presentation.SetText ShipSet(SetCount), 2, 78
            AS400.Presentation.SendKeys "[Enter]"
            WaitForReady AS400, Exception
            
            '2nd level loop: Perform on each page
            EndPageLoop = False
            Do
            
                '3rd level loop: Perform on each line
                i = StartSize
                Do
                
                    GetTextString = vbNullString
                    GetTextString = Trim(AS400.Presentation.GetText(i, 47, 4))
                    
                    CheckForTarget = vbNullString
                    CheckForTarget = Trim(AS400.Presentation.GetText(i, 77, 3))
                
                    'If the line is empty...
                    If Len(GetTextString) < 2 Then
                        Exit Do
                        
                    'ElseIf the line refers to electives or assessments...
                    ElseIf GetTextString = "ELEC" Or GetTextString = "M1PR" Then
                        'Do Nothing
                        
                    'ElseIf the line includes the target status...
                    ElseIf CheckForTarget = Target Then
                        AS400.Presentation.SetText "1", i, 3
                        
                    Else
                        'Do Nothing
                    End If
                
                    i = i + 1
                    
                Loop While i <= MaxSize
            
                GetTextString = vbNullString
                GetTextString = Trim(AS400.Presentation.GetText(23, 47, 4))
                
                If Len(GetTextString) < 2 Then
                
                    WaitForReady AS400, Exception
                    AS400.Presentation.SendKeys "[Enter]"
                    WaitForReady AS400, Exception
                    
                    EndPageLoop = True
                    
                Else
                
                    WaitForReady AS400, Exception
                    AS400.Presentation.SendKeys "[pagedn]"
                    WaitForReady AS400, Exception
                    
                End If
                 
            Loop Until EndPageLoop = True
            
        Next SetCount
        
        AS400.Presentation.SendKeys "[pf3]"
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Option: "Prevent Shipment Release"
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Else
    
        AS400.Presentation.SetText SID, 4, 22
        AS400.Presentation.SendKeys "[Enter]"
        WaitForReady AS400, Exception
        
        '1st level loop: Perform on each page
        EndPageLoop = False
        Do
        
            '2nd level loop: Perform on each line
            i = StartSize
            Do
            
                GetTextString = vbNullString
                GetTextString = Trim(AS400.Presentation.GetText(i, 47, 4))
                
                'If the line is empty...
                If Len(GetTextString) < 2 Then
                    Exit Do
                    
                'ElseIf the line refers to electives or assessments...
                ElseIf GetTextString = "ELEC" Or GetTextString = "M1PR" Then
                    'Do Nothing
                    
                Else
                    AS400.Presentation.SetText "1", i, 3
                End If
                
                i = i + 1
                
            Loop While i <= MaxSize
                    
            GetTextString = vbNullString
            GetTextString = Trim(AS400.Presentation.GetText(23, 47, 4))
            
            If Len(GetTextString) < 2 Then
            
                WaitForReady AS400, Exception
                AS400.Presentation.SendKeys "[Enter]"
                WaitForReady AS400, Exception
                
                EndPageLoop = True
                
            Else
            
                WaitForReady AS400, Exception
                AS400.Presentation.SendKeys "[pagedn]"
                WaitForReady AS400, Exception
                
            End If
            
        Loop Until EndPageLoop = True
        
        AS400.Presentation.SendKeys "[pf3]"
    
    End If

Cleanup:

    TurnOffExcelDefaults Exception
    Exception.Pop

Exit Sub

NavigationFailure:
    If NavigateToHome(AS400, Exception) = False Then
        AppActivate "Microsoft excel"
        MsgBox "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
            "Please navigate manually to the home screen, then attempt to run the application again.", _
            vbCritical, "Navigation Failure"
    End If
GoTo Cleanup

NavigationError:
    AppActivate "Microsoft excel"
    MsgBox "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
        "Please navigate manually to the home screen, then attempt to run the application again.", _
        vbCritical, "Navigation Error"
Resume Cleanup

ArrayFailure:
    AppActivate "Microsoft excel"
    MsgBox "A shipment value has not been recognized, and the application has been skipped." & _
        vbNewLine & vbNewLine & "Please inform the VBA support team before proceeding again with the shipment application", _
        vbCritical, "Shipments not recognized"
GoTo Cleanup

End Sub
