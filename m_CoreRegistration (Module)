''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' m_CoreRegistration (Module)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Option Explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RunRegistration (Public Sub)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub RunRegistration(ByVal RegForm As MSForms.UserForm)

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' General variables
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    Dim StartTime As Variant
    StartTime = Timer
    
    Dim GetTextString As String
    GetTextString = vbNullString

    Dim RedMessage As String
    RedMessage = vbTab & "FINAL CONFIRMATION" & vbNewLine & vbNewLine & _
        "One or more errors have occurred. Please resolve the following issues before proceeding." & vbNewLine & vbNewLine
    Dim BlueMessage As String
    BlueMessage = vbTab & "FINAL CONFIRMATION" & vbNewLine & vbNewLine & _
        "Please confirm the following information before proceeding with the enrollment script." & vbNewLine & vbNewLine
    
    Dim Forward As Long
    Forward = 1
    
    Dim Status As Object
    Set Status = RegForm.Status_Textbox
    Status.Value = "...Performing the pre-enrollment check..."

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Set the class modules
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    On Error GoTo ClassError

    Dim Exception As cException
    Set Exception = New cException
    Exception.Push "Procedure: m_CoreRegistration.RunRegistration"
    
    Dim StudentFile As cStudentFile
    Set StudentFile = New cStudentFile
    
    Dim AS400 As cAS400
    Set AS400 = New cAS400
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Loads the class objects
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    TurnOffExcelDefaults Exception
    
    On Error GoTo UserformError
    LoadUserformObjects StudentFile, RegForm, Exception
    
    On Error GoTo AS400Error
    InitializeAS400 AS400, StudentFile.GetSession, Exception
    AS400.Handling = Trim(StudentFile.GetAutomation)
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
PreEnrollmentCheck:
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    
    If ApproveRegistration(StudentFile, AS400, Exception, RedMessage, BlueMessage) = False Then
        GoTo Cleanup
    End If
    
    RedMessage = vbNullString
    BlueMessage = vbNullString
    
    Status.Value = "...Navigating to Enrollment..."
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Set the subsequent settings based on the user's selection
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    
    Select Case AS400.Handling
        Case "Fast"
            On Error GoTo AcknowledgeError
            AS400.Milliseconds = 75
        Case "Slow"
            On Error GoTo AcknowledgeError
            AS400.Milliseconds = 900
        Case "StopEach"
            On Error GoTo AcknowledgeError
            AS400.Milliseconds = 100
        Case "Debug"
            On Error GoTo 0
            AS400.Milliseconds = 200
        Case "Test"
            On Error GoTo 0
            GoTo HiddenTestOption
        Case "test"
            On Error GoTo 0
            GoTo HiddenTestOption
        Case Else
            On Error GoTo 0
            AS400.Milliseconds = 400
    End Select
    
    AS400.Metrics.Active = True
    
    If NavigateToHome(AS400, Exception) = False Then GoTo NavigationFailure
    
    'Address Book w/ Parent option
    AS400.Presentation.SetText "1", 19, 7
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    AS400.Presentation.SetText "3", 19, 7
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    GetTextString = AS400.Presentation.GetText(1, 2, 5)
    If Not GetTextString = "01051" Then GoTo NavigationFailure
    GetTextString = vbNullString
    
    Status.Value = "...Creating SID..."
    
    'Action Code
    AS400.Presentation.SetText "A", 3, 19              'Action Code
    'First-Middle-Last Name
    AS400.Presentation.SetText StudentFile.GetMailingName, 4, 19
    'Last-First-Middle Name
    AS400.Presentation.SetText StudentFile.GetAlphaName, 6, 19

    'Search Type
    AS400.Presentation.SetText "TR", 14, 19
    'Parent #
    AS400.Presentation.SetText StudentFile.GetParentCode, 17, 55
    'Prefix Code
    AS400.Presentation.SetText StudentFile.GetPrefixCode, 23, 19
    'Long Number
    If Len(StudentFile.GetLongNumber) >= 1 Then
        AS400.Presentation.SetText StudentFile.GetLongNumber, 2, 60
    End If
    
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    'StudentID#
    GetTextString = AS400.Presentation.GetText(1, 3, 6)
    If Not GetTextString = "010512" Then GoTo NavigationFailure
    GetTextString = vbNullString
    WaitForReady AS400, Exception
    StudentFile.StudentID = Trim(AS400.Presentation.GetText(4, 32, 8))
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    AppActivate "Microsoft Excel"
    MsgBox "Student ID:" & vbTab & StudentFile.StudentID, vbInformation, "Automation"
    AS400.Metrics.Active = True
    
    Status.Value = "...Navigating the Enter/F3 sequence..."
    
    'Enter-F3 sequence
    '#1
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    '#2
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    '#3
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    '#4
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    
    'Web Times
    AS400.Presentation.SetText StudentFile.GetWebStartTime, 13, 23
    AS400.Presentation.SetText StudentFile.GetWebEndTime, 14, 23
    
    '#5
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation

    'School Type
    AS400.Presentation.SetText "HS", 6, 23
    'Degree Code
    AS400.Presentation.SetText "N", 10, 23
    'Gender
    AS400.Presentation.SetText StudentFile.GetGender, 19, 57
    
    '#6
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    
    'Back to the address book
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    
    Status.Value = "...Entering contact information..."
    
    'Phone number
    If Len(StudentFile.GetPhoneNumber) >= 1 Then
        If ProceedToNextScreen("[pf12]", AS400, Exception) = True Then GoTo EndAutomation
        AS400.Presentation.SetText "C", 4, 21
        AS400.Presentation.SetText StudentFile.GetAreaCode, 11, 9
        AS400.Presentation.SetText StudentFile.GetPhoneNumber, 11, 16
        WaitForReady AS400, Exception
        AS400.Presentation.SendKeys "[Enter]"
        WaitForReady AS400, Exception
        AS400.Presentation.SendKeys "[pf3]"
        WaitForReady AS400, Exception
    End If
    
    'Email
    AS400.Presentation.SendKeys "[pf5]"
    WaitForReady AS400, Exception
    AS400.Presentation.SendKeys "[pf4]"
    WaitForReady AS400, Exception
    AS400.Presentation.SetText "C", 3, 18
    AS400.Presentation.SetText "E", 11, 62
    AS400.Presentation.SetText StudentFile.GetEmail, 10, 4
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    Status.Value = "...Navigating to Registration..."
    
    AS400.Presentation.SendKeys "[pf3]"
    WaitForReady AS400, Exception
    AS400.Presentation.SendKeys "[pf3]"
    WaitForReady AS400, Exception
    
    If NavigateToHome(AS400, Exception) = False Then GoTo MidEnrollmentNavigationFailure
    
    'Full program enrollment
    AS400.Presentation.SetText "20", 19, 7
    WaitForReady AS400, Exception
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    
    Status.Value = "...Entering the registration information..."
    
    'PS screen 66300
    AS400.Presentation.SetText "A", 3, 20
    AS400.Presentation.SetText StudentFile.StudentID, 4, 20
    AS400.Presentation.SetText StudentFile.GetSalesperson, 6, 20
    AS400.Presentation.SetText StudentFile.GetProgramNumber, 7, 20
    
    If Len(StudentFile.GetPONumber) >= 1 Then
        AS400.Presentation.SetText StudentFile.GetPONumber, 8, 20
    End If
    
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    'Check for PS screen 66302
    GetTextString = Trim(AS400.Presentation.GetText(7, 32, 5))
    WaitForReady AS400, Exception
    If GetTextString = "66302" Then
        AS400.Presentation.SendKeys "[Enter]"
        WaitForReady AS400, Exception
    End If
    GetTextString = vbNullString
    
    'Tuition
    AS400.Presentation.SetText "PA", 11, 11
    AS400.Presentation.SetText "TUI", 11, 16
    AS400.Presentation.SetText StudentFile.GetProgramTuition, 11, 54
    
    If ProceedToNextScreen("[Enter]", AS400, Exception) = True Then GoTo EndAutomation
    
    Status.Value = "...Finalizing Enrollment..."
    
    'Enter payment terms
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    AS400.Presentation.SendKeys "[Enter]"
    
    If ProceedToNextScreen("[pf3]", AS400, Exception) = True Then GoTo EndAutomation
    If ProceedToNextScreen("[pf11]", AS400, Exception) = True Then GoTo EndAutomation
    
    Status.Value = "...Navigating to Education..."
    
    'Navigate to Education Inquiry
    AS400.Presentation.SendKeys "[pf3]"
    WaitForReady AS400, Exception
    AS400.Presentation.SetText "1", 19, 7
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    AS400.Presentation.SetText "15", 19, 7
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    AS400.Presentation.SetText StudentFile.StudentID, 3, 13
    AS400.Presentation.SendKeys "[Enter]"
    WaitForReady AS400, Exception
    
    AppActivate "Microsoft Excel"
    MsgBox "Automation Complete" & vbNewLine & _
        "Student ID:" & vbTab & StudentFile.StudentID & vbNewLine & vbNewLine & _
        "Total Elapsed Time: " & (Timer - StartTime), vbInformation, "Automation"
    
Cleanup:

    TurnOnExcelDefaults Exception
    Exception.Pop
    
Exit Sub

ClassError:
    RedMessage = RedMessage & "[m_CoreRegistration.RunRegistration]" & Space(1) & _
        "> A critical error has occurred while initializing the class modules:" & vbNewLine & _
        "Error #" & Err.Number & Space(1) & Err.Description & vbNewLine & vbNewLine
Resume Next

UserformError:
    Exception.ErrFlag = True
    RedMessage = RedMessage & Exception.StackTop & Space(1) & _
        "> A userform object has failed to load" & Space(1) & "Error #" & Err.Number & vbNewLine & vbNewLine
Resume Next

AS400Error:
    Exception.ErrFlag = True
    RedMessage = RedMessage & Exception.StackTop & Space(1) & _
        "> The AS400 has failed to initialize" & vbNewLine & _
        "Error #" & Err.Number & Space(1) & Err.Description & vbNewLine & vbNewLine
Resume Next

AcknowledgeError:
    Exception.ErrFlag = True
    MsgBox "An unexpected error has occurred during the automation, and the application has been stopped." & _
    vbNewLine & vbNewLine & "Please discover where the automation has stopped, and determine " & _
    "if you need to proceed manually from here." & vbNewLine & vbNewLine & _
    "Call Stack Position:" & vbTab & Exception.StackTop & vbNewLine & _
    "Err Number:" & vbTab & Err.Number & vbNewLine & "Err Message:" & Err.Description, _
    vbCritical, "An unknown error has occurred"
GoTo Cleanup

EndAutomation:
    MsgBox "The automation has been canceled" & vbNewLine & vbNewLine & Exception.StackTop, _
        vbInformation, "Enrollment"
GoTo Cleanup

NavigationFailure:
    If NavigateToHome(AS400, Exception) = False Then
        MsgBox "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
            "Please navigate manually to the home screen, then attempt to run the application again." & _
            vbNewLine & vbNewLine & Exception.StackTop, vbCritical, "Navigation Failure"
    End If
GoTo Cleanup

MidEnrollmentNavigationFailure:
    Dim MsgStr As String
    MsgStr = "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
            "Confirm that the Address Book information has been entered successfully, then " & _
            "proceed with the tuition registration manually." & vbNewLine & vbNewLine & Exception.StackTop
    If Exception.ErrFlag = True Then
        MsgStr = MsgStr & vbTab & "Err Number: " & Exception.ErrNumber & vbNewLine & Exception.ErrDescription
    End If
    If NavigateToHome(AS400, Exception) = False Then
            MsgBox MsgStr, vbCritical, "Navigation Failure"
    End If
GoTo Cleanup

HiddenTestOption:
    RunUnitCheck StudentFile, AS400, Exception
GoTo Cleanup

End Sub


