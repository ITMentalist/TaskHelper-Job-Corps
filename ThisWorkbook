''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ThisWorkbook (worksheet)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Option Explicit

Sub EnrollmentAppButton_Click()

    form_TransferCredits.Show

End Sub

Sub TransferCreditsButton_Click()

    form_TransferCredits.Show
    
End Sub

Sub LicenseInfo_Button_Click()

    ThisWorkbook.FollowHyperlink "http://www.gnu.org/licenses/gpl-3.0.en.html"
 
End Sub

Sub ShipmentsButton_Click()

    Dim sendShipments As Boolean: sendShipments = False
    Dim returnValue As Integer: returnValue = 0
    
    Dim SID As String: SID = vbNullString
    Dim errorMessage As String: errorMessage = vbNullString
    Dim sessionLetter As String: sessionLetter = vbNullString
    
    Dim Exception As cException
    Set Exception = New cException
    Exception.Push "Procedure: ThisWorkbook.ShipmentsButton_Click"
    
    Exception.Message = "Presenting the shipment dialogue..."
    
    returnValue = MsgBox("*** Should shipments be sent for the following program? ***" & _
        vbNewLine & vbNewLine & vbNewLine & vbNewLine & _
        Space(3) & "Click 'Yes' to send the shipments." & vbNewLine & vbNewLine & _
        Space(3) & "Click 'No' to prevent the shipments." & vbNewLine & vbNewLine & _
        Space(3) & "Press 'Cancel' to end the application.", vbYesNoCancel, "Manage Shipments")
        
    Select Case returnValue
        Case vbYes
            sendShipments = True
        Case vbNo
            sendShipments = False
        Case vbCancel
            GoTo Cleanup
        Case Else
            GoTo Cleanup
    End Select
    
    Exception.Message = "Requesting the Student ID#..."
    
    SID = Application.InputBox("Enter the Student ID#")
    
    If SID = "" Then
        GoTo Cleanup
    ElseIf Len(SID) < 8 Or Len(SID) > 8 Or Not IsNumeric(SID) Then
        GoTo SIDFailure
    End If
    
    Exception.Message = "Requesting the session letter..."
    
    On Error GoTo AS400Error
    sessionLetter = Application.InputBox("Enter the AS/400 Session")
    
    If sessionLetter = "" Then
        GoTo Cleanup
    ElseIf Len(sessionLetter) > 1 Then
        GoTo SessionLetterLength
    End If
    
    Exception.Message = "Initializing the AS/400..."

    Dim AS400 As cAS400
    Set AS400 = New cAS400

    InitializeApp AS400, sessionLetter, Exception
    AS400.Time = 25

    Exception.Message = "Calling the shipments script..."

    On Error GoTo UnManagedError
    'ManageShipments AS400, SID, SendShipments, Exception

Cleanup:

    Exception.Message = vbNullString
    Exception.Pop

Exit Sub

SIDFailure:
    
    errorMessage = "Procedure cancelled: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted: " & Exception.Message & vbNewLine & vbNewLine & _
                   "The Student ID# is either missing or invalid." & _
                   "" & vbNewLine & vbNewLine
    MsgBox errorMessage, vbInformation, "Procedure cancelled (Invalid Student ID#)"
    
GoTo Cleanup

SessionLetterLength:

    errorMessage = "Procedure cancelled: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted: " & Exception.Message & vbNewLine & vbNewLine & _
                   "The AS/400 session letter is either missing or invalid." & _
                   "" & vbNewLine & vbNewLine
    MsgBox errorMessage, vbInformation, "Procedure cancelled (Invalid AS/400 session letter)"
    
GoTo Cleanup


AS400Error:

    errorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution: Please ensure the AS/400 is active, and the session letter " & _
                   "is valid and correct, before proceeding." & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    MsgBox errorMessage, vbCritical, "An error occurred while connecting to the AS/400"
    
Resume Cleanup

UnManagedError:

    errorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "An error occurred during primary execution. " & _
                   "No resolution is available at this time." & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    MsgBox errorMessage, vbCritical, "An error occurred while connecting to the AS/400"
    
Resume Cleanup

End Sub
