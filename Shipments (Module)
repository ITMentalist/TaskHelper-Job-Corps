''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Shipments (Module)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Option Explicit

'AS/400 static values
    Const StartSize As Long = 12
    Const MaxSize As Long = 23

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ManageShipments
'Assumes the passed arguments are valid entries
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub ManageShipments(ByVal AS400 As cAS400, ByVal SID As String, ByVal SendShipments As Boolean, ByRef Exception As cException)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Purpose:      TBD

' Input:        TBD

' Output:       TBD

' Assumptions:  TBD

' Handles Errors:   Yes
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Exception.Push "Shipments.ManageShipments"
    
    '''''''''''''''''''''''''''
    ' General variables
    '''''''''''''''''''''''''''
    
    Dim EndPageLoop As Boolean: EndPageLoop = False
    
    Dim SetCount As Integer: SetCount = 0
    Dim Pages As Integer: Pages = 0

    Dim i As Long: i = 0
    
    'AS/400 modular values
    Entry = vbNullString
    Row = 0
    Col = 0
    Length = 0
    
    GetTextString = vbNullString
    
    Dim CheckForTarget As String: CheckForTarget = vbNullString
    Dim Target As String: Target = vbNullString

    Dim ShipSet() As Variant
    ShipSet() = Array("540", "560", "620")

    '''''''''''''''''''''''''''
    ' Loads the required objects/information
    '''''''''''''''''''''''''''
    TurnOffExcelDefaults Exception

    AS400.Handling = "Fast"
    AS400.Time = 25
    
    '''''''''''''''''''''''''''
    ' Navigation
    '''''''''''''''''''''''''''
    On Error GoTo NavigationError
    Entry = "": Row = 1: Col = 3: Length = 5
    GetTextString = AS400.GetText(Row, Col, Length)
    
    If Not GetTextString = "42040" Then

        If NavigateToHome(AS400, Exception) = False Then GoTo NavigationFailure
    
        Entry = "1": Row = 19: Col = 7: Length = 0
        AS400.SetText Entry, Row, Col
        AS400.SendKeys "[Enter]"
        Pause AS400, Exception
    
        Entry = "8": Row = 19: Col = 7: Length = 0
        AS400.SetText Entry, Row, Col
        AS400.SendKeys "[Enter]"
        Pause AS400, Exception
        
    Else
        AS400.SendKeys "[pf3]"
    End If
    
    If SendShipments = False Then
        Entry = "17": Row = 19: Col = 7: Length = 0
        AS400.SetText Entry, Row, Col
    Else
        Entry = "18": Row = 19: Col = 7: Length = 0
        AS400.SetText Entry, Row, Col
    End If
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    '''''''''''''''''''''''''''
    ' Option: "Prevent Shipment Release"
    '''''''''''''''''''''''''''
    On Error Resume Next
    If SendShipments = False Then
           
        '1st level loop: Perform on each shipment status
        For SetCount = LBound(ShipSet) To UBound(ShipSet)
        
            Select Case ShipSet(SetCount)
            
                Case "540"
                    Target = "539"
                Case "560"
                    Target = "540"
                Case "620"
                    Target = "560"
                Case Else
                    GoTo ArrayFailure
                    
            End Select
            
            Entry = SID: Row = 4: Col = 22: Length = 0
            AS400.SetText Entry, Row, Col
            Entry = ShipSet(SetCount): Row = 2: Col = 78: Length = 0
            AS400.SetText Entry, Row, Col
            
            AS400.SendKeys "[Enter]"
            Pause AS400, Exception
            
            '2nd level loop: Perform on each page
            EndPageLoop = False
            Do
            
                '3rd level loop: Perform on each line
                i = StartSize
                Do
                
                    GetTextString = vbNullString
                    Entry = "": Row = i: Col = 47: Length = 4
                    GetTextString = Trim(AS400.GetText(Row, Col, Length))
                    
                    CheckForTarget = vbNullString
                    Entry = "": Row = i: Col = 77: Length = 3
                    CheckForTarget = Trim(AS400.GetText(Row, Col, Length))
                
                    'If the line is empty...
                    If Len(GetTextString) < 2 Then
                        Exit Do
                        
                    'ElseIf the line refers to electives or assessments...
                    ElseIf GetTextString = "ELEC" Or GetTextString = "M1PR" Then
                        'Do Nothing
                        
                    'ElseIf the line includes the target status...
                    ElseIf CheckForTarget = Target Then
                        Entry = "1": Row = i: Col = 3: Length = 0
                        AS400.SetText Entry, Row, Col
                        
                    Else
                        'Do Nothing
                    End If
                
                    i = i + 1
                    
                Loop While i <= MaxSize
            
                GetTextString = vbNullString
                Entry = "": Row = 23: Col = 47: Length = 4
                GetTextString = Trim(AS400.GetText(Row, Col, Length))
                
                If Len(GetTextString) < 2 Then
                
                    Pause AS400, Exception
                    AS400.SendKeys "[Enter]"
                    Pause AS400, Exception
                    
                    EndPageLoop = True
                    
                Else
                
                    Pause AS400, Exception
                    AS400.SendKeys "[pagedn]"
                    Pause AS400, Exception
                    
                End If
                 
            Loop Until EndPageLoop = True
            
        Next SetCount
        
        AS400.SendKeys "[pf3]"
    
    '''''''''''''''''''''''''''
    ' Option: "Prevent Shipment Release"
    '''''''''''''''''''''''''''
    Else
    
        Entry = SID: Row = 4: Col = 22: Length = 0
        AS400.SetText Entry, Row, Col
        AS400.SendKeys "[Enter]"
        Pause AS400, Exception
        
        '1st level loop: Perform on each page
        EndPageLoop = False
        Do
        
            '2nd level loop: Perform on each line
            i = StartSize
            Do
            
                GetTextString = vbNullString
                Entry = "": Row = i: Col = 47: Length = 4
                GetTextString = Trim(AS400.GetText(Row, Col, Length))
                
                'If the line is empty...
                If Len(GetTextString) < 2 Then
                    Exit Do
                    
                'ElseIf the line refers to electives or assessments...
                ElseIf GetTextString = "ELEC" Or GetTextString = "M1PR" Then
                    'Do Nothing
                    
                Else
                    Entry = "1": Row = i: Col = 3: Length = 0
                    AS400.SetText Entry, Row, Col
                End If
                
                i = i + 1
                
            Loop While i <= MaxSize
                    
            GetTextString = vbNullString
            Entry = "": Row = 23: Col = 47: Length = 4
            GetTextString = Trim(AS400.GetText(Row, Col, Length))
            
            If Len(GetTextString) < 2 Then
            
                Pause AS400, Exception
                AS400.SendKeys "[Enter]"
                Pause AS400, Exception
                
                EndPageLoop = True
                
            Else
            
                Pause AS400, Exception
                AS400.SendKeys "[pagedn]"
                Pause AS400, Exception
                
            End If
            
        Loop Until EndPageLoop = True
        
        AS400.SendKeys "[pf3]"
    
    End If
    
'''''''''''''''''''''''''''
' Cleanup
'''''''''''''''''''''''''''
Cleanup:

    TurnOffExcelDefaults Exception
    Exception.Pop

Exit Sub

'''''''''''''''''''''''''''
' Error Handling
'''''''''''''''''''''''''''
NavigationFailure:
    If NavigateToHome(AS400, Exception) = False Then
        AppActivate "Excel"
        MsgBox "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
            "Please navigate manually to the home screen, then attempt to run the application again.", _
            vbCritical, "Navigation Failure"
    End If
GoTo Cleanup

NavigationError:
    AppActivate "Excel"
    MsgBox "The AS/400 has failed to reach the correct screen." & vbNewLine & vbNewLine & _
        "Please navigate manually to the home screen, then attempt to run the application again.", _
        vbCritical, "Navigation Error"
Resume Cleanup

ArrayFailure:
    AppActivate "Excel"
    MsgBox "A shipment value has not been recognized, and the application has been skipped." & _
        vbNewLine & vbNewLine & "Please inform the VBA support team before proceeding again with the shipment application", _
        vbCritical, "Shipments not recognized"
GoTo Cleanup

End Sub
