''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Registration (Module)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Contains the core procedures for handling agency enrollments
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

Option Explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' RunRegistration (Public Sub)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Public Sub RunRegistration(ByVal RegForm As MSForms.UserForm)
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Purpose:      Excel-To-AS400 glue procedure; performs an _
                    enrollment from a userform to AS/400 _
                    client-side access

' Input:        The registration userform object

' Output:       Procedural actions

' Assumptions:  The user is reasonably attempting a legitimate enrollment _
                No changes are made to the AS/400 that would cause this _
                    procedure to "run off the rails"
                    
' AS/400 required:  Yes

' Handles Errors:   Yes
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    If DebugFlag Then On Error GoTo 0 Else On Error GoTo VariablesError

    '''''''''''''''''''''''''''
    ' General variables
    '''''''''''''''''''''''''''
    Dim StartTime As Single: StartTime = Timer
    
    'AS/400 static values
    Const ModLength As Long = 8
    
    'AS/400 modular values
    Entry = vbNullString
    Row = 0
    Col = 0
    Length = 0
    
    GetTextString = vbNullString
    Dim ErrorMessage As String: ErrorMessage = vbNullString

    Dim RedMessage As String: RedMessage = vbTab & "FINAL CONFIRMATION" & _
        vbNewLine & vbNewLine & "One or more errors have occurred. Please" & _
        "resolve the following issues before proceeding." & vbNewLine & vbNewLine
    
    Dim BlueMessage As String: BlueMessage = vbTab & "FINAL CONFIRMATION" & _
        vbNewLine & vbNewLine & "Please confirm the following information " & _
        "before proceeding with the enrollment script." & vbNewLine & vbNewLine

    Dim Status As Object
    Set Status = RegForm.Status_Textbox
    Status.Value = "...   ...   ...   Performing the pre-enrollment check   ...   ...   ..."
    
    '''''''''''''''''''''''''''
    ' Set the class modules
    '''''''''''''''''''''''''''
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo ClassError

    Dim Exception As cException
    Set Exception = New cException
    Exception.Push "Registration.RunRegistration"
    
    Dim StudentFile As cStudentFile
    Set StudentFile = New cStudentFile
    
    Dim AS400 As cAS400
    Set AS400 = New cAS400
    
    '''''''''''''''''''''''''''
    ' Loads the class objects
    '''''''''''''''''''''''''''
    TurnOffExcelDefaults Exception
    
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo UserformError
    LoadUserformObjects StudentFile, RegForm, Exception
    
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo AS400Error
    InitializeApp AS400, StudentFile.GetSession, Exception
    
    '''''''''''''''''''''''''''
    ' Pre-enrollment check
    '''''''''''''''''''''''''''
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo ErrorOnEnrollmentCheck
    If ApproveRegistration(StudentFile, RedMessage, BlueMessage, Exception) = False Then
        GoTo Cleanup
    End If
    
    RedMessage = vbNullString
    BlueMessage = vbNullString
    
    Status.Value = "...   ...   ...   Performing Enrollment  (Please wait)   ...   ...   ..."
    
    '''''''''''''''''''''''''''
    ' Additional Settings
    '''''''''''''''''''''''''''
    AS400.Time = 25
    AS400.Metrics.Active = True
    
    '''''''''''''''''''''''''''
    ' Navigation
    '''''''''''''''''''''''''''
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo NavigationError
    NavigateHome AS400, Exception
    
    Exception.Message = "Navigating to the Address Book..."
    
    'Address Book w/ Parent option
    Entry = "1": Row = 19: Col = 7: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    Entry = "3": Row = 19: Col = 7: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    Entry = "": Row = 1: Col = 2: Length = 5
    GetTextString = AS400.GetText(Row, Col, Length)
    
    If Not GetTextString = "01051" Then GoTo NavigationFailure
    GetTextString = vbNullString
    
    '''''''''''''''''''''''''''
    ' Address Book registration
    '''''''''''''''''''''''''''
    If DebugFlag Then On Error GoTo 0 Else On Error GoTo ExecutionError
    
    Exception.Message = "Adding to the Address Book..."
    
    'Action Code
    Entry = "A": Row = 3: Col = 19: Length = 0
    AS400.SetText Entry, Row, Col
    
    'First-Middle-Last Name
    Entry = StudentFile.GetMailingName: Row = 4: Col = 19: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Last-First-Middle Name
    Entry = StudentFile.GetAlphaName: Row = 6: Col = 19: Length = 0
    AS400.SetText Entry, Row, Col

    'Search Type
    Entry = "TR": Row = 14: Col = 19: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Parent #
    Entry = StudentFile.GetParentCode: Row = 17: Col = 55: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Prefix Code
    Entry = StudentFile.GetPrefixCode: Row = 23: Col = 19: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Long Number
    If Len(StudentFile.GetLongNumber) >= 1 Then
        Entry = StudentFile.GetLongNumber: Row = 2: Col = 60: Length = 0
        AS400.SetText Entry, Row, Col
    End If
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    Exception.Message = "Pulling the Student ID#..."
    
    'StudentID#
    Entry = "": Row = 1: Col = 3: Length = 6
    GetTextString = AS400.GetText(Row, Col, Length)
    
    If Not GetTextString = "010512" Then GoTo NavigationFailure
    GetTextString = vbNullString
    Pause AS400, Exception
    
    Entry = "": Row = 4: Col = 32: Length = ModLength
    StudentFile.StudentID = Trim(AS400.GetText(Row, Col, Length))
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    AppActivate "Excel"
    MsgBox "Student ID:" & vbTab & StudentFile.StudentID, vbInformation, "Automation"
    AS400.Metrics.Active = True
    
    Exception.Message = "Navigating the Enter/F3 sequence"
    
    'Enter/F3 sequence
    '#1
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    '#2
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    '#3
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    '#4
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    
    Pause AS400, Exception
    
    'Web Times
    Entry = StudentFile.GetWebStartTime: Row = 13: Col = 23: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.GetWebEndTime: Row = 14: Col = 23: Length = 0
    AS400.SetText Entry, Row, Col
    
    '#5
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    
    Pause AS400, Exception
    
    Exception.Message = "Adding the account type information..."

    'School Type
    Entry = "HS": Row = 6: Col = 23: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Degree Code
    Entry = "N": Row = 10: Col = 23: Length = 0
    AS400.SetText Entry, Row, Col
    
    'Gender
    Entry = StudentFile.GetGender: Row = 19: Col = 57: Length = 0
    AS400.SetText Entry, Row, Col
    
    '#6
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[pf3]"
    
    Exception.Message = "Adding contact information..."
    
    'Back to the address book
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    'Phone number
    If Len(StudentFile.GetPhoneNumber) >= 1 Then
    
        AS400.SendKeys "[pf12]"
        
        Pause AS400, Exception
        
        Entry = "C": Row = 4: Col = 21: Length = 0
        AS400.SetText Entry, Row, Col
        
        Entry = StudentFile.GetAreaCode: Row = 11: Col = 9: Length = 0
        AS400.SetText Entry, Row, Col
        
        Entry = StudentFile.GetPhoneNumber: Row = 11: Col = 16: Length = 0
        AS400.SetText Entry, Row, Col
        
        AS400.SendKeys "[Enter]"
        AS400.SendKeys "[pf3]"
        
    End If
    
    'Email
    AS400.SendKeys "[pf5]"
    AS400.SendKeys "[pf4]"
    
    Pause AS400, Exception
    
    Entry = "C": Row = 3: Col = 18: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = "E": Row = 11: Col = 62: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.GetEmail: Row = 10: Col = 4: Length = 0
    AS400.SetText Entry, Row, Col
    AS400.SendKeys "[Enter]"
    
    AS400.SendKeys "[pf3]"
    AS400.SendKeys "[pf3]"
    Pause AS400, Exception
    
    NavigateHome AS400, Exception

    '''''''''''''''''''''''''''
    ' Course Registration
    '''''''''''''''''''''''''''
    
    Exception.Message = "Entering the course information..."

    'Full program enrollment
    Entry = "20": Row = 19: Col = 7: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    
    Pause AS400, Exception
    
    'PS screen 66300
    Entry = "A": Row = 3: Col = 20: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.StudentID: Row = 4: Col = 20: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.GetSalesperson: Row = 6: Col = 20: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.GetProgramNumber: Row = 7: Col = 20: Length = 0
    AS400.SetText Entry, Row, Col
    
    If Len(StudentFile.GetPONumber) >= 1 Then
        Entry = StudentFile.GetPONumber: Row = 8: Col = 20: Length = 0
        AS400.SetText Entry, Row, Col
    End If
    
    AS400.SendKeys "[Enter]"
    
    Pause AS400, Exception
    
    'Check for PS screen 66302
    Entry = "": Row = 7: Col = 32: Length = 5
    GetTextString = Trim(AS400.GetText(Row, Col, Length))
    
    If GetTextString = "66302" Then
        AS400.SendKeys "[Enter]"
        Pause AS400, Exception
    End If
    GetTextString = vbNullString
    
    Exception.Message = "Entering the tuition information..."
    
    'Tuition
    Entry = "PA": Row = 11: Col = 11: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = "TUI": Row = 11: Col = 16: Length = 0
    AS400.SetText Entry, Row, Col
    
    Entry = StudentFile.GetProgramTuition: Row = 11: Col = 54: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    
    'Enter payment terms
    AS400.SendKeys "[Enter]"
    AS400.SendKeys "[Enter]"
    
    AS400.SendKeys "[pf3]"
    
    Pause AS400, Exception
    
    Exception.Message = "Pressing the final enrollment key..."
    
    'Final Enrollment key
    AS400.SendKeys "[pf11]"
    
    Status.Value = "...   ...   ...  (Please Wait)  ...   ...   ..."
    
    Pause AS400, Exception
    
    Exception.Message = "Navigating to the Education screen..."
    
    'Navigate to Education Inquiry
    AS400.SendKeys "[pf3]"
    Pause AS400, Exception
    
    Entry = "1": Row = 19: Col = 7: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    Entry = "15": Row = 19: Col = 7: Length = 0
    AS400.SetText Entry, Row, Col
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    Entry = StudentFile.StudentID: Row = 3: Col = 13: Length = 0
    AS400.SetText Entry, Row, Col
    
    AS400.SendKeys "[Enter]"
    Pause AS400, Exception
    
    '''''''''''''''''''''''''''
    ' Transfer Credits option
    '''''''''''''''''''''''''''
    Exception.Message = "Transfer Credits dialogue box."
    
    AppActivate "Excel"
    If MsgBox("Automation Complete:" & " Student ID #" & StudentFile.StudentID & vbNewLine & vbNewLine & _
        "Total Elapsed Time: " & vbTab & (Timer - StartTime) & " seconds" & vbNewLine & vbNewLine & _
        "DO YOU WISH TO APPLY TRANSFER CREDITS?", vbYesNo, "Automation Complete - Apply TR?") = vbYes Then
        
        Unload RegForm
        
        u_TransferCredits.Show
        
    End If
    
'''''''''''''''''''''''''''
' Cleanup
'''''''''''''''''''''''''''
Cleanup:

    TurnOnExcelDefaults Exception
    Exception.Message = vbNullString
    Exception.Pop
    
Exit Sub

'''''''''''''''''''''''''''
' Error Handling
'''''''''''''''''''''''''''

VariablesError:

    ErrorMessage = "Error occurred during procedure: " & "Main.ExtractCourseNames" & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & "Setting the variables and custom objects" & vbNewLine & vbNewLine & _
                   "Recommended resolution:" & vbTab & "Unavailable" & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred during procedure initialization"

Resume Cleanup

UserformError:
    
    ErrorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution:" & vbTab & "Unavailable" & _
                   "" & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred while accessing the userform"
    
Resume Cleanup

AS400Error:

    ErrorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution: Please ensure the AS/400 is active, and the session letter " & _
                   "is valid and correct, before proceeding." & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred while connecting to the AS/400"
    
Resume Cleanup

ErrorOnEnrollmentCheck:

    ErrorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution:" & vbTab & "Unavailable" & _
                   "" & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred during the pre-enrollment check"

Resume Cleanup

NavigationError:

    ErrorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution: Please ensure the AS/400 is on a valid screen " & _
                   "before proceeding." & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred while navigating the AS/400"

Resume Cleanup

NavigationFailure:

    ErrorMessage = "Failure occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution: Please ensure the AS/400 is on a valid screen " & _
                   "before proceeding." & vbNewLine & vbNewLine
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "A failure occurred while navigating the AS/400"

GoTo Cleanup

ExecutionError:

    ErrorMessage = "Error occurred during procedure: " & Exception.StackTop & vbNewLine & vbNewLine & _
                   "Last action attempted by script: " & Exception.Message & vbNewLine & vbNewLine & _
                   "Recommended resolution: An unknown error has occurred during the procedure." & vbNewLine & vbNewLine & _
                   "Please determine where the procedure has ended, then with the enrollment manually." & vbNewLine & vbNewLine & _
                   "Error # " & Str(Err.Number) & ": " & Err.Description
    AppActivate "Excel"
    MsgBox ErrorMessage, vbCritical, "An error occurred during normal execution"

Resume Cleanup

End Sub
